// Generic Gradle project

apply plugin: 'java'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.11'
    }
}

dependencies {
    implementation 'com.google.protobuf:protobuf-javalite:3.8.0'
    testImplementation 'junit:junit:4.13'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.68'
    implementation 'org.conscrypt:conscrypt-openjdk-uber:2.5.1'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

configurations {
    customConfig.extendsFrom implementation
}

sourceSets.main.resources { srcDirs = ["src/assets"]; include "**/*.txt" }

jar {
    manifest {
        attributes(
            "Main-Class": "com.android.apksigner.ApkSignerTool"
        )
    }
    from sourceSets.main.output
    from { configurations.customConfig.collect { it.isDirectory() ? it : zipTree(it) } } {
        exclude(
            'META-INF/*.RSA',
            'META-INF/*.SF',
            'META-INF/*.DSA',
            'META-INF/LICENSE',
            'META-INF/LICENSE.txt',
            'META-INF/MSFTSIG.SF',
            'META_INF/ECLIPSE_.SF',
            'META-INF/*.kotlin_module'
        )
    }
}